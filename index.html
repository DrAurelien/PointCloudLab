<!DOCTYPE html>

<html>
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
	<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/4.4.0/css/font-awesome.min.css">
	<link rel="stylesheet" type="text/css" href="style.css">
	<script id="VertexShader" type="x-shader/x-vertex">
		attribute vec3 VertexPosition;
		attribute vec3 NormalVector;
		attribute float ScalarValue;
		attribute float FlagValue;
		uniform mat4 ModelView;
		uniform mat4 Projection;
		uniform mat4 ShapeTransform;

		varying highp vec3 Position;
		varying highp vec3 Normal;
		varying highp float Scalar;
		varying lowp float Flag;

		void main(void) {
			gl_PointSize = 5.0;
			gl_Position = Projection * ModelView * ShapeTransform * vec4(VertexPosition, 1.0);
			Position = VertexPosition;

			Normal = vec3((ShapeTransform * vec4(NormalVector, 0.0)).xyz);

			Scalar = ScalarValue;
			Flag = FlagValue;
		}
	</script>
	<script id="FragmentShader" type="x-shader/x-fragment">
		//Lighting model
		uniform  highp vec3 EyePosition;
		uniform  highp vec3 LightPositions[8];
		uniform  highp vec3 LightColors[8];
		uniform lowp int NbLights;
		uniform  highp vec3 Color;
		uniform highp float DiffuseCoef;
		uniform  highp float AmbiantCoef;
		uniform  highp float SpecularCoef;
		uniform  highp float GlossyPow;
		uniform lowp int UseNormals;
		uniform lowp int UseScalars;
		uniform lowp int UseFlags;
		uniform highp float MinScalarValue;
		uniform highp float MaxScalarValue;
		uniform  highp vec3 MinScalarColor;
		uniform  highp vec3 MaxScalarColor;

		varying highp vec3 Position;
		varying highp vec3 Normal;
		varying highp float Scalar;
		varying lowp float Flag;

		void main(void) {
			highp float ambiant;
			highp float diffuse;
			highp float specular;
			highp vec3 color = Color;

			if(UseScalars == 1 && MinScalarValue < MaxScalarValue)
			{
				highp float value = (Scalar - MinScalarValue) / (MaxScalarValue-MinScalarValue);
				if(value >= 0.0 && value <= 1.0)
				{
					color = (1.0-value)*MinScalarColor + value*MaxScalarColor;
				}
				else {
					discard;
				}
			}

			if(UseFlags == 1 && Flag > 0.0)
			{
				gl_FragColor = vec4(1, 1, 1, 1);
			}
			else if(UseNormals == 0)
			{
				gl_FragColor = vec4(color, 1);
			}
			else
			{
				//Phong lighting
				highp vec3 result = vec3(0.0);
				for(int index = 0; index <= 8; index++)
				{
					if(index < NbLights)
					{
						highp vec3 LightPosition = LightPositions[index];
						highp vec3 LightColor = LightColors[index];

						highp vec3 eyeVect = EyePosition - Position;
						eyeVect /= length(eyeVect);
						highp vec3 lightVect = LightPosition - Position;
						lightVect /= length(lightVect);
						highp vec3 reflVec = (2.0 * dot(Normal, lightVect) * Normal)-lightVect;
						reflVec /= length(reflVec);

						ambiant = AmbiantCoef;
						diffuse = max(DiffuseCoef * dot(Normal, lightVect), 0.0);
						specular = SpecularCoef * pow(max(dot(eyeVect, reflVec), 0.0), GlossyPow);

						result += (LightColor * ambiant) + (color * diffuse) + (LightColor * specular);
					}
				}

				gl_FragColor = vec4(result, 1);
			}
		}
	</script>
	<script type="text/javascript" src="PointCloudLab.js"></script>
</head>
<body onload="PCLApp.Run();">
</body>
</html>